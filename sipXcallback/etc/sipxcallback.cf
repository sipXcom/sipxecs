# Copyright (c) 2015 sipXcom, Inc. All rights reserved.
# Contributed to SIPfoundry under a Contributor Agreement

# This software is free software; you can redistribute it and/or modify it under
# the terms of the Affero General Public License (AGPL) as published by the
# Free Software Foundation; either version 3 of the License, or (at your option)
# any later version.

# This software is distributed in the hope that it will be useful, but WITHOUT
# ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
# FOR A PARTICULAR PURPOSE. See the GNU Affero General Public License for more
# details.

#
# sipxcallback configuration and process management
#
bundle agent sipxcallback {
  methods:
    sipxcallback::
      "any" usebundle => "sipxcallback_config";
    any::
      "any" usebundle => "sipxcallback_setup";
      "any" usebundle => "sipxcallback_running";
}

bundle agent sipxcallback_config {

  files:
    any::
      "$(sipx.SIPX_CONFDIR)/sipxcallback.properties"
        comment => "install call back on busy config $(this.promiser)",
        create => "true",
        perms => m("644"),
        edit_line => sipxcallback_properties_contents,
        classes => if_repaired("restart_sipxcallback");

      "$(sipx.SIPX_CONFDIR)/sipxcallback/log4j.properties"
        comment => "configure callback log4j $(this.promiser)",
        create => "true",
        perms => m("644"),
        edit_line => sipxcallback_log4j_properties_contents;
}

bundle edit_line sipxcallback_properties_contents {
  insert_lines:
    any::
      "$(sipx.SIPX_CFDATA)/$(sipx.location_id)/sipxcallback.properties.part"
        insert_type => "file";
      "callback.sipxchangeDomainName=$(sipx.domain)";
      "log.file=$(sipx.SIPX_LOGDIR)/sipxcallback.log";
  delete_lines:
    any::
      ".*";
}

bundle edit_line sipxcallback_log4j_properties_contents {
  insert_lines:
    any::
      "$(sipx.SIPX_CFDATA)/$(sipx.location_id)/log4j-callback.properties.part"
        insert_type => "file";
	  "log4j.rootLogger=warn, file";
      "log4j.appender.file=org.sipfoundry.commons.log4j.SipFoundryAppender";
      "log4j.appender.file.File=$(sipx.SIPX_LOGDIR)/sipxcallback.log";
      "log4j.appender.file.layout=org.sipfoundry.commons.log4j.SipFoundryLayout";
      "log4j.appender.file.layout.facility=sipXcallback";
  delete_lines:
    any::
      ".*";
}

bundle agent sipxcallback_setup {
  methods:
    !src.sipxcallback::
      "any" usebundle => rh_chkconfig_status("sipxcallback on");
    !src.!sipxcallback::
      "any" usebundle => rh_chkconfig_status("sipxcallback off");
}

bundle agent sipxcallback_running {
  vars:
    any::
      "service_command" string => ".*\s-Dprocname=sipxcallback\s.*";

  methods:
    any::
      "any" usebundle => find_sipxservice_by_command_regex("$(service_command)","sipxcallback_running");

  commands:
    (!sipxcallback|stop_sipxecs).sipxcallback_running::
      "$(sipx.SIPX_SERVICEDIR)/sipxcallback"
        comment => "stop sipxcallback",
        args => "stop";

    sipxcallback.!sipxcallback_running.!stop_sipxecs::
      "$(sipx.SIPX_SERVICEDIR)/sipxcallback"
        comment => "start sipxcallback",
        args => "start";

    sipxcallback.sipxcallback_running.(restart_sipxecs|restart_sipxcallback)::
      "$(sipx.SIPX_SERVICEDIR)/sipxcallback"
        comment => "restart sipxcallback",
        args => "restart";
}