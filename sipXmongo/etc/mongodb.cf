# Copyright (C) 2012 eZuce Inc., certain elements licensed under a Contributor Agreement.
# Contributors retain copyright to elements licensed under a Contributor Agreement.
# Licensed to the User under the AGPL license.

# Mongo configuration for sipXecs

bundle agent mongodb {
  methods:
    mongod::
      "any" usebundle => "mongodb_config";
    any::
      "any" usebundle => "mongodb_running";
      # at this moment, all setups get mongo client config
      "any" usebundle => "mongodb_client";
}

bundle agent mongodb_config {
  vars:
     mongod|primary::
      "conf[replSet]" string => "sipxecs";
      "conf[bind_ip]" string => "127.0.0.1";
      "conf[port]" string => "27017";

  files:
    mongod|primary::
      "/etc/mongod.conf"
        create => "false",
        perms => m("644"),
        edit_line => set_variable_values("mongodb_config.conf"),
        classes => if_repaired("restart_mongodb");
}

# not meant to replace monitoring service that will restart crashed mongo
# this just ensures it's running incase it was shutdown for some reason
bundle agent mongodb_running {
  vars:
    any::
      "service" string => "/etc/init.d/mongod";
      "service_command" string => "/usr/bin/mongod";

  methods:
    any::
      "any" usebundle => find_service_by_command("$(service_command)", "mongodb_running","mongod");

  classes:
    any::
      "enabled" or => { "mongodb", "primary" };

  commands:
    mongodb_running.!enabled::
      "$(service)"
        args => "stop",
	action => track("mongodb_stop");

    (restart_mongodb|!mongodb_running).enabled::
      "$(service)"
        args => "start",
	action => track("mongodb_start");

    mongodb_running.restart_mongodb.enabled::
      "$(service)"
        args => "restart",
	action => track("mongodb_restart");  
}

# Not working at the momemnt.  Mongod is not creating a pid file
bundle agent mongodb_repair {
  files:
    any::
      "$(sipx.crontabs)/root"
        create => "true",
	perms => m("0600"),
        edit_line => mongodb_auto_repair("$(this.promiser)");
}


# Once a minute, will check if mongod died and needs repair/restarting
# If mongod was stopped normally, this script leaves mongod stopped.
bundle edit_line mongodb_auto_repair(filename) {
  classes:
    "install_repairer" not => regline(".*$(sipx.SIPX_BINDIR)/mongodb-repair.*", "$(filename)");

  insert_lines:
    install_repairer::
      "* * * * * $(sipx.SIPX_BINDIR)/mongodb-repair";
}

bundle agent mongodb_client {

  classes:
    "ini_file" expression => fileexists("$(sipx.SIPX_CFDATA)/mongo-client.ini");

  files:
    ini_file::
      # should not change often, we don't want to restart mongod unless
      # absolutely nec..  So sipXconfig should take care the client config
      # file it generates has the same checksum as the original or not generate
      # ini file in cfdata unless it's different.
      "$(sipx.SIPX_CONFDIR)/mongo-client.ini"
        create => "false",
        perms => m("644"),
	copy_from => copy_from_cfdata("mongo-client.ini"),
        classes => if_repaired("restart_mongodb"),
	action => track("monto-client.ini");
}
