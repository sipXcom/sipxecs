# Copyright (C) 2012 eZuce Inc., certain elements licensed under a Contributor Agreement.
# Contributors retain copyright to elements licensed under a Contributor Agreement.
# Licensed to the User under the AGPL license.

# Mongo configuration for sipXecs

bundle agent mongodb {
  methods:
    mongod::
      "any" usebundle => "mongodb_config";
    any::
      "any" usebundle => "mongodb_running";
      # at this moment, all setups get mongo client config
      "any" usebundle => "mongodb_client";
}

bundle agent mongodb_config {
  vars:
     mongod::
       "server[replSet]" string => "sipxecs";
       "server[bind_ip]" string => "127.0.0.1";
       "server[port]" string => "27017";

  files:
    mongod::
      "/etc/mongod.conf"
        create => "false",
        perms => m("644"),
        edit_line => set_variable_values("mongodb_config.server"),
        classes => if_repaired("restart_mongodb");

    any::
      "$(sipx.SIPX_CONFDIR)/mongo-client.ini",
        create => "true",
        perms => m("644"),
	edit_line => mongo_client_config,
        classes => if_repaired("mongodb_client_reconnect");
}

bundle edit_line mongo_client_config {
  insert_lines:
     # TODO: read this from $(sipx.*) properties
     "connectionUrl=mongodb://localhost:27017/?slaveOk=true";
     "connectionString=localhost:27017";

  delete_lines:
    ".*";
}

# not meant to replace monitoring service that will restart crashed mongo
# this just ensures it's running incase it was shutdown for some reason
bundle agent mongodb_running {
  vars:
    any::
      "service" string => "/etc/init.d/mongod";

  methods:
    any::
      "any" usebundle => find_service_by_command("$(service_command)", "mongodb_running","mongod");

  commands:
    mongodb_running.!mongod::
      "$(service)"
        args => "stop",
	action => track("mongodb_stop");

    (restart_mongodb|!mongodb_running).mongod::
      "$(service)"
        args => "start",
	action => track("mongodb_start");

    mongodb_running.restart_mongodb.mongod::
      "$(service)"
        args => "restart",
	action => track("mongodb_restart");  
}

# Not working at the momemnt.  Mongod is not creating a pid file
bundle agent mongodb_repair {
  files:
    any::
      "$(sipx.crontabs)/root"
        create => "true",
	perms => m("0600"),
        edit_line => mongodb_auto_repair("$(this.promiser)");
}


# Once a minute, will check if mongod died and needs repair/restarting
# If mongod was stopped normally, this script leaves mongod stopped.
bundle edit_line mongodb_auto_repair(filename) {
  classes:
    "install_repairer" not => regline(".*$(sipx.SIPX_BINDIR)/mongodb-repair.*", "$(filename)");

  insert_lines:
    install_repairer::
      "* * * * * $(sipx.SIPX_BINDIR)/mongodb-repair";
}

bundle agent mongodb_client {

  classes:
    "ini_file" expression => fileexists("$(sipx.SIPX_CFDATA)/mongo-client.ini");

  files:
    ini_file::
      # should not change often, we don't want to restart mongod unless
      # absolutely nec..  So sipXconfig should take care the client config
      # file it generates has the same checksum as the original or not generate
      # ini file in cfdata unless it's different.
      "$(sipx.SIPX_CONFDIR)/mongo-client.ini"
        create => "false",
        perms => m("644"),
	copy_from => copy_from_cfdata("mongo-client.ini"),
        classes => if_repaired("restart_mongodb"),
	action => track("monto-client.ini");
}
