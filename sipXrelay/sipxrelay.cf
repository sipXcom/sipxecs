# Copyright (C) 2012 eZuce Inc., certain elements licensed under a Contributor Agreement.
# Contributors retain copyright to elements licensed under a Contributor Agreement.
# Licensed to the User under the AGPL license.

#
# sipXrelay configuration and process management
#
bundle agent sipxrelay {
  methods:
    sipxrelay::
      "any" usebundle => "sipxrelay_config";
      "any" usebundle => "sipxrelay_setup";
    any::
      "any" usebundle => "sipxrelay_running";
}

bundle agent sipxrelay_config {
  files:
    any::
      "$(sipx.SIPX_CONFDIR)/nattraversalrules.xml"
        create => "true",
        perms => m("644"),
	copy_from => copy_from_cfdata("$(sipx.location_id)/nattraversalrules.xml"),
        classes => if_repaired("restart_sipxrelay"),
	action => track("$(this.promiser)");
}

bundle agent sipxrelay_setup {
  methods:
    !src::
      "any" usebundle => rh_chkconfig_status("sipxrelay");
}

bundle agent sipxrelay_running {
  vars:
    any::
      "service_command" string => ".*\s-Dprocname=sipxrelay\s.*";

  methods:
    any::
      "any" usebundle => find_sipxservice_by_command_regex("$(service_command)","sipxrelay_running");

  commands:
    sipxrelay_running.!sipxrelay::
      "$(sipx.SIPX_SERVICEDIR)/sipxrelay"
        args => "stop",
	action => track("sipxrelay_stop");

    !sipxrelay_running.sipxrelay::
      "$(sipx.SIPX_SERVICEDIR)/sipxrelay"
        args => "start",
	action => track("sipxrelay_start");

    restart_sipxrelay::
      "$(sipx.SIPX_SERVICEDIR)/sipxrelay"
        args => "restart",
	action => track("sipxrelay_restart");
}
